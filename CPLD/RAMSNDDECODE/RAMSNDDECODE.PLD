Name     RAMSNDDECODE ;
PartNo   00 ;
Date     03/07/2022 ;
Revision 01 ;
Designer SMD ;
Company  Machina Speculatrix ;
Assembly ;
Location ;
Device   f1502ispplcc44 ;

/*

This provides decoding for extended RAM and a 65C22 VIA driving a sound
generator.

The memory sits at address $8000. The decoding allows us to select one of 16
8K banks at this address.

The bank is selected by writing the value 0-15 to the address $BFE0. This
CPLD provides the decoding for that address as well as a register (BSEL) of four
D-type flip-flops that hold the selected value.

This register connects to address lines A13-A16 of the RAM, thus selecting the
effective address of the bank.

The VIA is at address $BFC0.
*/

/* --------------- INPUTS --------------------------------------------------- */
PIN 43 = CLK;                              /* PHI2 clock                      */
PIN 41 = RWB;                              /* Read!Write signal from CPU      */
PIN [8,6,5,4] = [D3..0] ;                  /* Data bus to select mem bank     */
PIN [24,21..16,14,12,11,9] = [A15..5];     /* Address bus                     */

/* --------------- OUTPUTS -------------------------------------------------- */
PIN [33,34,36,37] = [BSEL3..0] ;           /* Bank select register            */
PIN 39 = !RAM_EN;                          /* RAM chip enable - active low    */
PIN 28 = !VIA_EN;                          /* VIA chip enable                 */

NODE BSEL_EN;                              /* Enable latching for bank select */

RAM_EN = A15 & !A14 & !A13;                /* Enable RAM at $8000             */
VIA_EN = A15 & !A14 & [A13..6]:7 & !A5;    /* Enable VIA at $BFC0             */

/* BSEL_EN is selected when the address set is $BFE0. This is the case when
we're writing a value to select the memory bank. */

BSEL_EN = A15 & !A14 & [A13..5]:& ;      /* Decodes for 32-bit block at $BFE0 */

/* The four D flip-flops in the BSEL register have their clocks enabled.
The flip-flop is set to the appropriate value when CLK transitions low - ie, on
the falling edge. The flip-flop is also enabled only when RWB is low (ie doing
write operations) BSEL_EN is active.

The flip-flops will set according to the inputs on D0-D3.
When the clock goes high again, these values are latched. When we no longer
have $BFE0 as the address, the values will remain latched.

These four flip-flops are connected to A13-A16 on the RAM chip. They're
not attached to the address bus - so they don't interfere with anything else.
*/
BSEL3.d = D3 ;                    /* Value from data bus                      */
BSEL3.ck = !CLK;                  /* Act on falling edge of clock             */
BSEL3.ce = !RWB & BSEL_EN;        /* Only during writes & with $BFE0 selected */
BSEL2.d = D2 ;
BSEL2.ck = !CLK;
BSEL2.ce = !RWB & BSEL_EN;
BSEL1.d = D1 ;
BSEL1.ck = !CLK;
BSEL1.ce = !RWB & BSEL_EN;
BSEL0.d = D0 ;
BSEL0.ck = !CLK;
BSEL0.ce = !RWB & BSEL_EN;

Name     FLASHDECODE ;
PartNo   00 ;
Date     25/05/2022 ;
Revision 01 ;
Designer SMD ;
Company  Machina Speculatrix ;
Assembly ;
Location ;
Device   f1502tqfp44 ;

/* *************** INPUT PINS ******************** */
PIN 2  = CLK ; 								/* PHI2 clock                     */ 
PIN [33,34,35,42,43,44,5,6] = [A15..8] ;	/* Addr pins to enable I/O        */
PIN 8 = ASEV;								/* Addr pins to select I/O bank   */
PIN 10 = ASIX;								/*   "							  */
PIN 11 = AFIV;								/*   "							  */
PIN [30,31,40,3] = [D3..0] ;				/* Data bus	to select mem bank	  */

/* *************** OUTPUT PINS ******************** */
PIN [21..18,15..12] = [IO7..0] ;            /* For I/O output. Active LOW     */ 
PIN 22 = !FLASH_EN ;						/* To /CE pin on Flash IC         */ 
PIN [28,27,25,23] = [LATCH3..0] ;			/* To top 4 addr pins on Flash IC */ 

NODE IO_EN ;                                /* Buried logic, used internally  */
NODE LATCH_EN;								/*  "							  */

/* *** FLASH MEMORY *** */
LATCH_EN = A15 & !A14 & [A13..8]:& & ASEV & ASIX & AFIV ;

LATCH3.d = D3 ;								/* D-latch input 				  */
LATCH3.ck = CLK & LATCH_EN ;				/* Address-qualified clock        */
LATCH2.d = D2 ;								/* D-latch input 				  */
LATCH2.ck = CLK & LATCH_EN ;				/* Address-qualified clock        */
LATCH1.d = D1 ;								/* D-latch input 				  */
LATCH1.ck = CLK & LATCH_EN ;				/* Address-qualified clock        */
LATCH0.d = D0 ;								/* D-latch input 				  */
LATCH0.ck = CLK & LATCH_EN ;				/* Address-qualified clock        */

FLASH_EN = A15 & !A14 & !A13 ;				/* Chip enable					  */

/* *** EXTENDED I/O *** */
IO_EN = A15 & !A14 & [A13..8]:& & CLK;			/* PHI2-qualified addr decode */
FIELD DEV_ADDR = [IO_EN, ASEV, ASIX, AFIV] ;	/* Address state              */ 
FIELD DEV_SELECT = [IO7..0] ;					/* I/O output state           */

TABLE DEV_ADDR => DEV_SELECT {  /* Match address state to I/O output state    */
'b'0XXX => 'b'11111111 ; 		/* If IO_EN is false, then no output selected */
'b'1000 => 'b'11111110 ;        /* Selected output is LOW                     */
'b'1001 => 'b'11111101 ;		/*    "										  */
'b'1010 => 'b'11111011 ;		/*    "										  */
'b'1011 => 'b'11110111 ;		/*    "										  */
'b'1100 => 'b'11101111 ;		/*    "										  */
'b'1101 => 'b'11011111 ;		/*    "										  */
'b'1110 => 'b'10111111 ;		/*    "										  */
'b'1111 => 'b'01111111 ;		/*    "										  */
}
